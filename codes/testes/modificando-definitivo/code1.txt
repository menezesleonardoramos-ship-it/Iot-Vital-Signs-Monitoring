#include <Wire.h>        // Biblioteca para comunicacao I2C
#include <U8g2lib.h>     // Biblioteca para controle de displays displayOLED
#include "MAX30105.h"    // Biblioteca para MAX30105.h 
#include <WiFi.h>        // Biblioteca para conexao Wifi na ESP
#include <Firebase_ESP_Client.h>  // Biblioteca para comunicacao com o Firebase
#include <time.h> // Biblioteca para obter o tempo (usando timestamp)

// Display OLED
U8G2_SSD1306_128X64_NONAME_F_HW_I2C displayOLED(U8G2_R0, U8X8_PIN_NONE);

// MAX30102 (BPM + SpO2)
MAX30105 sensorMAX30102;

// BPM 
const byte bpm_media = 6;  
int bpmAvg = 0;               // BPM médio final
float bufferBPM[bpm_media];   // Buffer de valores de BPM
byte indiceBPM = 0;           // Índice do buffer circular


// Variáveis auxiliares para detecção do batimento
static long mediaIR = 0;
static long irAnterior = 0;
static bool rising = false;
static unsigned long tempoUltimoBatimento = 0;

// SpO2 
#define SPO2_SIZE 3        // Média móvel de SpO2
int spo2Buffer[SPO2_SIZE];
byte spo2Index = 0;
int spo2 = 0;              // Valor final de SpO2
unsigned long ultimoCalculoSpO2 = 0;

// MAX30205 (Temperatura)
#define MAX30205_ADDRESS 0x48 
float temperatura = 36.5;       // Valor inicial estimado

// WIFI & FIREBASE 
#define WIFI_SSID "PET Convidados"
#define WIFI_PASSWORD "petagregado"
#define DATABASE_URL "projeto-integradora-ii-default-rtdb.firebaseio.com"
#define DATABASE_SECRET "uC0SyV22Gi3CEPqkslCwpJtPB1Kr2EAiH9JmrMVD"

FirebaseData fbdo;
FirebaseAuth auth;
FirebaseConfig config;

bool firebaseOnline = false;   // Status da conexão Firebase
bool sensoresProntos = false;   // Indica quando os sensores estão estáveis

// LOADING 
bool telaInicial = true;
unsigned long inicioSistema = 0;
const unsigned long TEMPO_LOADING = 15000;

// TIMERS
unsigned long ultimoEnvioFirebase = 0;
const unsigned long INTERVALO_FIREBASE = 5000; // Envio a cada 5s


// Exibe uma tela de loading enquanto os sensores estabilizam
void loadingScreen() {
  unsigned long elapsed = millis() - inicioSistema;
  int progresso = map(elapsed, 0, TEMPO_LOADING, 0, 104);
  progresso = constrain(progresso, 0, 104);

  displayOLED.clearBuffer();
  displayOLED.setFont(u8g2_font_6x10_tr);

  displayOLED.setCursor(14, 28);
  displayOLED.print("Carregando");

  displayOLED.setCursor(14, 40);
  displayOLED.print("Sinais Vitais");

  displayOLED.drawFrame(12, 54, 104, 6);
  displayOLED.drawBox(14, 56, progresso, 2);

  displayOLED.sendBuffer();
}

// SETUP 
void setup() {
  Serial.begin(115200);

  //Inicializa display
  Wire.begin(21, 22);
  Wire.setClock(100000);

  displayOLED.begin();


  // Inicializa sensor MAX30102
  sensorMAX30102.begin(Wire, I2C_SPEED_FAST);
  sensorMAX30102.setup(0x1F, 4, 2, 200, 215, 2048);
  sensorMAX30102.setPulseAmplitudeIR(0x0F);
  sensorMAX30102.setPulseAmplitudeRed(0x0F);

  // Conecta ao Wi-Fi
  WiFi.begin(WIFI_SSID, WIFI_PASSWORD);

  // Configura Firebase
  config.database_url = DATABASE_URL;
  config.signer.tokens.legacy_token = DATABASE_SECRET;
  Firebase.begin(&config, &auth);
  Firebase.reconnectWiFi(true);

  configTime(-3 * 3600, 0, "pool.ntp.org");

  inicioSistema = millis();
}


void loop() {

  // Caregamento 15s
  if (telaInicial) {
    loadingScreen();
    if (millis() - inicioSistema >= TEMPO_LOADING) {
      telaInicial = false;
      sensoresProntos = true;
    }
    return;
  }

  // Leitura dos sinais IR e RED
  long irValue = sensorMAX30102.getIR();
  long redValue = sensorMAX30102.getRed();
  unsigned long now = millis();

  // BPM 
  mediaIR = mediaIR * 0.95 + irValue * 0.05;
  long signal = irValue - mediaIR;

  // Detecta subida do sinal (batimento)
  if (signal > 1500 && irAnterior < signal && !rising) rising = true;
  
  // Detecta pico do batimento
  if (rising && signal < irAnterior) {
    rising = false;

    if (tempoUltimoBatimento > 0) {
      float bpm = 60000.0 / (now - tempoUltimoBatimento);
      if (bpm > 40 && bpm < 180) {
        bufferBPM[indiceBPM++] = bpm;
        indiceBPM %= bpm_media;

        bpmAvg = 0;
        for (byte i = 0; i < bpm_media; i++) bpmAvg += bufferBPM[i];
        bpmAvg /= bpm_media;
      }
    }
    tempoUltimoBatimento = now;
  }
  irAnterior = signal;

  // SpO2 
  if (millis() - ultimoCalculoSpO2 > 500) {

    static long irDC = 0;
    static long redDC = 0;

    irDC  = irDC  * 0.95 + irValue  * 0.05;
    redDC = redDC * 0.95 + redValue * 0.05;

    long irAC  = irValue  - irDC;
    long redAC = redValue - redDC;

    if (irAC > 300 && redAC > 300) {
      float ratio = (float(redAC) / redDC) / (float(irAC) / irDC);
      int s = constrain(110 - 25 * ratio, 85, 100);

      spo2Buffer[spo2Index++] = s;
      spo2Index %= SPO2_SIZE;

      int soma = 0;
      for (byte i = 0; i < SPO2_SIZE; i++) soma += spo2Buffer[i];
      spo2 = soma / SPO2_SIZE;
    }

    ultimoCalculoSpO2 = millis();
  }

  // TEMPERATURA 
  static unsigned long lastTemp = 0;
  if (millis() - lastTemp > 2000) {
    float t = readMAX30205();
    if (!isnan(t)) temperatura = temperatura * 0.8 + t * 0.2;
    lastTemp = millis();
  }

  updateDisplay();

  // FIREBASE 
  if (sensoresProntos && millis() - ultimoEnvioFirebase > INTERVALO_FIREBASE) {
    sendToFirebase();
    ultimoEnvioFirebase = millis();
  }
}


void updateDisplay() {
  displayOLED.clearBuffer();

  displayOLED.setFont(u8g2_font_5x8_tr);
  displayOLED.setCursor(12, 8);
  displayOLED.print("Sinais Vitais");

  displayOLED.setCursor(12, 16);
  displayOLED.print(firebaseOnline ? "Online" : "Offline");

  displayOLED.drawLine(0, 18, 127, 18);

  displayOLED.setFont(u8g2_font_6x10_tr);

  displayOLED.setCursor(0, 32);
  displayOLED.print("BPM: ");
  bpmAvg > 0 ? displayOLED.print(bpmAvg) : displayOLED.print("--");

  displayOLED.setCursor(0, 44);
  displayOLED.print("SpO2: ");
  spo2 > 0 ? displayOLED.print(spo2) : displayOLED.print("--");
  displayOLED.print(" %");

  displayOLED.setCursor(0, 58);
  displayOLED.print("Temp: ");
  displayOLED.print(temperatura, 1);
  displayOLED.print(" C");

  displayOLED.sendBuffer();
}


float readMAX30205() {
  Wire.beginTransmission(MAX30205_ADDRESS);
  Wire.write(0x00);
  if (Wire.endTransmission(false) != 0) return NAN;
  if (Wire.requestFrom(MAX30205_ADDRESS, 2) != 2) return NAN;

  int16_t raw = (Wire.read() << 8) | Wire.read();
  return raw * 0.00390625;
}


String getTimestamp() {
  struct tm timeinfo;
  if (!getLocalTime(&timeinfo)) return "sem_tempo";

  char buffer[25];
  strftime(buffer, sizeof(buffer), "%Y-%m-%d_%H-%M-%S", &timeinfo);
  return String(buffer);
}


void sendToFirebase() {
  if (!Firebase.ready()) {
    firebaseOnline = false;
    return;
  }

  bool ok = true;

  // Dados in real time (atuais)
  ok &= Firebase.RTDB.setFloat(&fbdo, "/atuais/temp", temperatura);
  ok &= Firebase.RTDB.setInt(&fbdo, "/atuais/bpm", bpmAvg);
  ok &= Firebase.RTDB.setInt(&fbdo, "/atuais/spo2", spo2);

  // HISTÓRICO 
  String timestamp = getTimestamp();
  String path = "/historico/" + timestamp;

  ok &= Firebase.RTDB.setFloat(&fbdo, path + "/temp", temperatura);
  ok &= Firebase.RTDB.setInt(&fbdo, path + "/bpm", bpmAvg);
  ok &= Firebase.RTDB.setInt(&fbdo, path + "/spo2", spo2);

  firebaseOnline = ok;
}
