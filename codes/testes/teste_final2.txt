#include <Wire.h>
#include <U8g2lib.h>
#include "MAX30105.h"
#include <WiFi.h>
#include <Firebase_ESP_Client.h>
#include <time.h>
#include <math.h>

// ================= OLED =================
U8G2_SSD1306_128X64_NONAME_F_HW_I2C u8g2(U8G2_R0, U8X8_PIN_NONE);

// ================= MAX30102 =================
MAX30105 particleSensor;

// ================= BPM =================
const byte RATE_SIZE = 6;
byte rates[RATE_SIZE];
byte rateIndex = 0;
int bpmAvg = 0;

static long irAvg = 0;
static long irPrev = 0;
static bool rising = false;
static unsigned long lastBeatTime = 0;

// ================= SpO2 =================
long redAvg = 0;
long irAvgSpO2 = 0;
int spo2 = 0;

// ================= MAX30205 =================
#define MAX30205_ADDRESS 0x48
float temperatura = 36.5;

// ================= WIFI & FIREBASE =================
#define WIFI_SSID "PET Convidados"
#define WIFI_PASSWORD "petagregado"
#define DATABASE_URL "projeto-integradora-ii-default-rtdb.firebaseio.com"
#define DATABASE_SECRET "uC0SyV22Gi3CEPqkslCwpJtPB1Kr2EAiH9JmrMVD"

FirebaseData fbdo;
FirebaseAuth auth;
FirebaseConfig config;

bool firebaseOnline = false;

// ================= LOADING =================
bool telaInicial = true;
unsigned long inicioSistema = 0;
const unsigned long TEMPO_LOADING = 15000; // 15 segundos

unsigned long lastFirebaseSend = 0;
const unsigned long FIREBASE_INTERVAL = 30000;

// =============================================================
// TELA DE LOADING
void loadingScreen() {
  unsigned long elapsed = millis() - inicioSistema;
  int progresso = map(elapsed, 0, TEMPO_LOADING, 0, 100);
  progresso = constrain(progresso, 0, 100);

  u8g2.clearBuffer();
  u8g2.setFont(u8g2_font_6x10_tr);

  u8g2.setCursor(10, 26);
  u8g2.print("Carregando");

  u8g2.setCursor(10, 38);
  u8g2.print("Sinais Vitais");

  u8g2.drawFrame(10, 52, 108, 8);
  u8g2.drawBox(12, 54, progresso, 4);

  u8g2.sendBuffer();
}

// =============================================================

void setup() {
  Serial.begin(115200);

  Wire.begin(21, 22);
  Wire.setClock(100000);

  u8g2.begin();
  u8g2.setFont(u8g2_font_6x10_tr);

  if (!particleSensor.begin(Wire, I2C_SPEED_FAST)) {
    while (1);
  }

  particleSensor.setup(0x1F, 4, 2, 200, 215, 2048);
  particleSensor.setPulseAmplitudeIR(0x0F);
  particleSensor.setPulseAmplitudeRed(0x0F);

  WiFi.begin(WIFI_SSID, WIFI_PASSWORD);

  config.database_url = DATABASE_URL;
  config.signer.tokens.legacy_token = DATABASE_SECRET;
  Firebase.begin(&config, &auth);
  Firebase.reconnectWiFi(true);

  configTime(-3 * 3600, 0, "pool.ntp.org");

  inicioSistema = millis();
}

// =============================================================

void loop() {
  // ================= LOADING FIXO 15s =================
  if (telaInicial) {
    loadingScreen();
    if (millis() - inicioSistema >= TEMPO_LOADING) {
      telaInicial = false;
    }
    return;
  }

  long irValue = particleSensor.getIR();
  long redValue = particleSensor.getRed();
  unsigned long now = millis();

  // ================= BPM =================
  irAvg = irAvg * 0.95 + irValue * 0.05;
  long signal = irValue - irAvg;

  if (signal > 1500 && irPrev < signal && !rising) rising = true;

  if (rising && signal < irPrev) {
    rising = false;

    if (lastBeatTime > 0) {
      unsigned long delta = now - lastBeatTime;
      float bpm = 60000.0 / delta;

      if (bpm > 40 && bpm < 180) {
        rates[rateIndex++] = (byte)bpm;
        rateIndex %= RATE_SIZE;

        bpmAvg = 0;
        for (byte i = 0; i < RATE_SIZE; i++) bpmAvg += rates[i];
        bpmAvg /= RATE_SIZE;
      }
    }
    lastBeatTime = now;
  }
  irPrev = signal;

  // ================= SpO2 =================
  irAvgSpO2 = irAvgSpO2 * 0.95 + irValue * 0.05;
  redAvg    = redAvg    * 0.95 + redValue * 0.05;

  if (irAvgSpO2 > 0 && redAvg > 0) {
    long irAC  = irValue  - irAvgSpO2;
    long redAC = redValue - redAvg;

    if (irAC > 500 && redAC > 500) {
      float ratio = (float(redAC) / redAvg) / (float(irAC) / irAvgSpO2);
      spo2 = constrain(110 - 25 * ratio, 85, 100);
    }
  }

  // ================= TEMPERATURA =================
  static unsigned long lastTemp = 0;
  if (millis() - lastTemp > 2000) {
    float t = readMAX30205();
    if (!isnan(t)) {
      temperatura = temperatura * 0.8 + t * 0.2;
    }
    lastTemp = millis();
  }

  updateDisplay();

  // ================= FIREBASE =================
  if (millis() - lastFirebaseSend > FIREBASE_INTERVAL) {
    sendToFirebase();
    lastFirebaseSend = millis();
  }
}

// =============================================================
// DISPLAY
void updateDisplay() {
  u8g2.clearBuffer();
  u8g2.setFont(u8g2_font_6x10_tr);

  u8g2.setCursor(0, 10);
  u8g2.print("Sinais vitais");

  u8g2.setCursor(78, 10); // afastado da borda
  u8g2.print(firebaseOnline ? "Online" : "Offline");

  u8g2.drawLine(0, 16, 127, 16);

  u8g2.setCursor(0, 30);
  u8g2.print("BPM: ");
  bpmAvg > 0 ? u8g2.print(bpmAvg) : u8g2.print("--");

  u8g2.setCursor(0, 42);
  u8g2.print("SpO2: ");
  spo2 > 0 ? u8g2.print(spo2) : u8g2.print("--");
  u8g2.print(" %");

  u8g2.setCursor(0, 54);
  u8g2.print("Temp: ");
  u8g2.print(temperatura, 1);
  u8g2.print(" C");

  u8g2.sendBuffer();
}

// =============================================================
// TEMPERATURA
float readMAX30205() {
  Wire.beginTransmission(MAX30205_ADDRESS);
  Wire.write(0x00);
  if (Wire.endTransmission(false) != 0) return NAN;
  if (Wire.requestFrom(MAX30205_ADDRESS, 2) != 2) return NAN;

  int16_t raw = (Wire.read() << 8) | Wire.read();
  return raw * 0.00390625;
}

// =============================================================
// FIREBASE
String getDateTimeKey() {
  struct tm timeinfo;
  if (!getLocalTime(&timeinfo)) return "sem_tempo";

  char buffer[20];
  strftime(buffer, sizeof(buffer), "%Y-%m-%d_%H-%M-%S", &timeinfo);
  return String(buffer);
}

void sendToFirebase() {
  if (!Firebase.ready()) {
    firebaseOnline = false;
    return;
  }

  bool ok = true;

  ok &= Firebase.RTDB.setFloat(&fbdo, "/atuais/temp", temperatura);
  ok &= Firebase.RTDB.setInt(&fbdo, "/atuais/bpm", bpmAvg);
  ok &= Firebase.RTDB.setInt(&fbdo, "/atuais/spo2", spo2);

  String basePath = "/historico/" + getDateTimeKey();
  ok &= Firebase.RTDB.setFloat(&fbdo, basePath + "/temp", temperatura);
  ok &= Firebase.RTDB.setInt(&fbdo, basePath + "/bpm", bpmAvg);
  ok &= Firebase.RTDB.setInt(&fbdo, basePath + "/spo2", spo2);

  firebaseOnline = ok;
}
